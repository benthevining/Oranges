# ======================================================================================
#    ____  _____            _   _  _____ ______  _____
#   / __ \|  __ \     /\   | \ | |/ ____|  ____|/ ____|
#  | |  | | |__) |   /  \  |  \| | |  __| |__  | (___
#  | |  | |  _  /   / /\ \ | . ` | | |_ |  __|  \___ \
#  | |__| | | \ \  / ____ \| |\  | |__| | |____ ____) |
#   \____/|_|  \_\/_/    \_\_| \_|\_____|______|_____/
#
#  This file is part of the Oranges open source CMake library and is licensed under the terms of the GNU Public License.
#
# ======================================================================================

#[=======================================================================[.rst:

OrangesUninstallTarget
-------------------------

When this module is included, it creates a target that uninstalls all of the project's installed files.

.. note::

    Inclusion style: Once globally, preferably from top-level project


Targets
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

``uninstall``

When built, removes all files listed in the project's install manifest (generated by the ``cmake --install`` command).


Variables
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

.. cmake:variable:: ORANGES_ADDITIONAL_INSTALL_MANIFEST_FILES

This variable may be populated with a list of file paths of additional install manifest files, such as those produced by Python or other tools.
An install manifest file must be a simple list of absolute paths of installed files, one per line.
Relative paths in ``ORANGES_ADDITIONAL_INSTALL_MANIFEST_FILES`` will be interpreted relative to ``CMAKE_BINARY_DIR``. (All paths *within* install manifest files must be absolute.)
This variable is empty by default.

#]=======================================================================]

include_guard (GLOBAL)

cmake_minimum_required (VERSION 3.22 FATAL_ERROR)

if (TARGET uninstall)
    message (AUTHOR_WARNING "uninstall target already exists!")
    return ()
endif ()

if (ORANGES_IN_GRAPHVIZ_CONFIG)
    return ()
endif ()

if (PROJECT_IS_TOP_LEVEL)
    message (DEBUG "Oranges - adding uninstall target in project ${PROJECT_NAME}")
else ()
    message (AUTHOR_WARNING "Creating uninstall target in non-top-level project ${PROJECT_NAME}!")
endif ()

set (ORANGES_ADDITIONAL_INSTALL_MANIFEST_FILES ""
     CACHE STRING "A list of additional install manifest files for the uninstall target to read.
    Each file must contain one absolute path to an installed file per line.")

set (configured_script "${CMAKE_BINARY_DIR}/uninstall.cmake")

configure_file ("${CMAKE_CURRENT_LIST_DIR}/scripts/uninstall.cmake" "${configured_script}" @ONLY)

add_custom_target (
    uninstall
    COMMAND "${CMAKE_COMMAND} -P ${configured_script}"
    COMMENT "Running uninstall..."
    WORKING_DIRECTORY "${CMAKE_BINARY_DIR}"
    VERBATIM USES_TERMINAL)

set_target_properties (uninstall PROPERTIES FOLDER Utility LABELS Utility XCODE_GENERATE_SCHEME OFF
                                            EchoString "Uninstalling...")

unset (configured_script)
